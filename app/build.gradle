/*
 * This file is part of Grocy Android.
 *
 * Grocy Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Grocy Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Grocy Android. If not, see http://www.gnu.org/licenses/.
 *
 * Copyright (c) 2020-2023 by Patrick Zedler and Dominic Zedler
 */

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
}

android {
    namespace 'xyz.zedler.patrick.grocy'
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "xyz.zedler.patrick.grocy"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 45
        versionName "3.1.1"
        resourceConfigurations += ['ca', 'cs', 'de', 'en', 'es', 'et', 'fr', 'hu', 'it', 'iw', 'nb', 'nl', 'pl', 'pt-rBR', 'pt-rPT', 'ru', 'sk', 'sv', 'uk', 'zh-rCN', 'zh-rTW']

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles {
                    getDefaultProguardFile('proguard-android-optimize.txt')
                    'proguard-rules.pro'
                }
            }
            debuggable false
        }
    }

    bundle {
        storeArchive {
            enable true
        }
    }
    
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    // Fix for Kotlin build error from some AndroidX dependencies
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // Navigation framework for easy navigation control using XML
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    // Android Room for database and RxJava for Async access of database
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.room:room-rxjava3:2.5.1'
    annotationProcessor 'androidx.room:room-compiler:2.5.1'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'

    // Nice material views
    // don't upgrade to 1.10 alpha, only change it to stable 1.9.0 when it has been released
    implementation 'com.google.android.material:material:1.9.0'

    // Volley for all general network requests and Glide for image loading
    // Netcipher for Tor support and Conscrypt for new TLS support on old Android devices
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'dev.gustavoavila:java-android-websocket-client:2.0.2'

    // GSON for converting JSON responses to Java objects
    implementation 'com.google.code.gson:gson:2.10.1'

    // FuzzyWuzzy for fuzzy search in product names and autocompletion
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'

    // Work for daily notifications
    implementation 'androidx.work:work-runtime:2.8.1'

    implementation 'androidx.webkit:webkit:1.6.1'

    // ZXing for barcode scanning
    //noinspection GradleDependency
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    // https://github.com/journeyapps/zxing-android-embedded#option-2-desugaring-advanced
    // prevents bug https://github.com/patzly/grocy-android/issues/425
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation "androidx.camera:camera-camera2:1.3.0-alpha05"
    implementation "androidx.camera:camera-lifecycle:1.3.0-alpha05"
    implementation 'androidx.camera:camera-view:1.3.0-alpha05'
    implementation 'androidx.camera:camera-mlkit-vision:1.3.0-alpha05'
    implementation 'com.google.mlkit:barcode-scanning:17.1.0'
}